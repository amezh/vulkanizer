#version 460

#extension GL_EXT_control_flow_attributes: require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_16bit_storage: require
#extension GL_NV_mesh_shader: require

#include "shader_common.h"

layout(local_size_x = kShaderGroupSizeNV) in;
layout(triangles, max_vertices = kMaxVerticesPerMeshlet, max_primitives = kMaxTrianglesPerMeshlet) out;

layout(binding = 0) readonly buffer PerDrawDataVector { PerDrawData perDrawDataVector[]; };
layout(binding = 1) readonly buffer DrawCommands { DrawCommand drawCommands[]; };
layout(binding = 2) readonly buffer Meshlets { Meshlet meshlets[]; };
layout(binding = 3) readonly buffer Meshes { Mesh meshes[]; };
layout(binding = 4) readonly buffer MeshletVertices { uint meshletVertices[]; };
layout(binding = 5) readonly buffer MeshletTriangles { uint meshletTriangles[]; };
layout(binding = 6) readonly buffer Vertices { Vertex vertices[]; };

taskNV in Task
{
	uint meshletIndices[kShaderGroupSizeNV];
} inTask;

layout(location = 0) out vec3 outColor[];

layout (push_constant) uniform block
{
    PerFrameData perFrameData;
};

vec3 getRandomColor(
	uint _seed)
{
	uint hash = (_seed ^ 61) ^ (_seed >> 16);
	hash = hash + (hash << 3);
	hash = hash ^ (hash >> 4);
	hash = hash * 0x27d4eb2d;
	hash = hash ^ (hash >> 15);
	return vec3(float(hash & 255),
				float((hash >> 8) & 255),
				float((hash >> 16) & 255)) / 255.0;
}

void main()
{
	uint groupIndex = gl_WorkGroupID.x;
	uint groupThreadIndex = gl_LocalInvocationID.x;
	uint meshletIndex = inTask.meshletIndices[groupIndex]; 
	
	uint drawIndex = drawCommands[gl_DrawID].drawIndex;
	PerDrawData perDrawData = perDrawDataVector[drawIndex];

	// This allows loop unrolling, since vertexLoops is known at compile time.
	const uint vertexLoops = (kMaxVerticesPerMeshlet + kShaderGroupSizeNV - 1) / kShaderGroupSizeNV;

	vec3 meshletColor = getRandomColor(meshletIndex);
	uint globalVertexOffset = meshes[perDrawData.meshIndex].vertexOffset;

	[[unroll]]
	for (uint loopIndex = 0; loopIndex < vertexLoops; ++loopIndex)
	{
		uint localVertexIndex = groupThreadIndex + loopIndex * kShaderGroupSizeNV;
		localVertexIndex = min(localVertexIndex, meshlets[meshletIndex].vertexCount - 1);

		uint vertexIndex = globalVertexOffset + meshletVertices[meshlets[meshletIndex].vertexOffset + localVertexIndex];
		
		vec3 position = vec3(
			vertices[vertexIndex].position[0],
			vertices[vertexIndex].position[1],
			vertices[vertexIndex].position[2]);

		vec2 normalXY = vec2(
			int(vertices[vertexIndex].normal[0]),
			int(vertices[vertexIndex].normal[1])) / 127.0 - 1.0;

		// Reconstruct normal vector Z component.
		// https://aras-p.info/texts/CompactNormalStorage.html
		float normalZ = max(/*epsilon*/ 1e-06, sqrt(1.0 - dot(normalXY, normalXY)));
		vec3 normal = mat3(perDrawData.model) * normalize(vec3(normalXY, normalZ));

		vec2 texCoord = vec2(
			vertices[vertexIndex].texCoord[0],
			vertices[vertexIndex].texCoord[1]);

		gl_MeshVerticesNV[localVertexIndex].gl_Position =
			perFrameData.viewProjection * perDrawData.model * vec4(position, 1.0);

		outColor[localVertexIndex] = 0.5 * (meshletColor + 0.5 * normal + 0.5);
	}

	uint packedTriangleOffset = meshlets[meshletIndex].triangleOffset / 4;
	uint packedTrianglesMax = (3 * meshlets[meshletIndex].triangleCount - 1) / 4;
	
	// This allows compiler loop unrolling.
	const uint triangleLoops = (3 * kMaxTrianglesPerMeshlet + 4 * kShaderGroupSizeNV - 1) / (kShaderGroupSizeNV * 4);
	
	[[unroll]]
	for (uint loopIndex = 0; loopIndex < triangleLoops; ++loopIndex)
	{
		uint localTriangleIndex = groupThreadIndex + loopIndex * kShaderGroupSizeNV;
		localTriangleIndex = min(localTriangleIndex, packedTrianglesMax);

		writePackedPrimitiveIndices4x8NV(4 * localTriangleIndex, meshletTriangles[packedTriangleOffset + localTriangleIndex]);
	}

	if (groupThreadIndex == 0)
	{
		gl_PrimitiveCountNV = meshlets[meshletIndex].triangleCount;
	}
}
